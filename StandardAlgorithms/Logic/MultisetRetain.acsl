
#ifndef MULTISETRETAIN_SPEC_INCLUDED
#define MULTISETRETAIN_SPEC_INCLUDED

#include "MultisetUnchanged.acsl"

/*@
  axiomatic MultisetRetain
  {
    predicate
    MultisetRetain{K,L}(value_type* a, integer n, value_type v) =
      Count{K}(a, 0, n, v) == Count{L}(a, 0, n, v);

    predicate
    MultisetRetainRest{K,L}(value_type* a, integer n, value_type  v) =
      \forall value_type x;
        x != v  ==>  MultisetRetain{K,L}(a, n, x);

    predicate
    MultisetRetainRest{K,L}(value_type* a, integer n, value_type  v, value_type w) =
      \forall value_type x;
        x != v  ==>  x != w  ==>  MultisetRetain{K,L}(a, n, x);

    lemma Multiset_UpdateRetain{K,L}:
      \forall value_type *a, v, integer m, n;
        0 <= m < n                         ==>
        \at(a[m],K) != v                   ==>
        \at(a[m],L) != v                   ==>
        MultisetUnchanged{K,L}(a, 0, m)    ==>
        MultisetUnchanged{K,L}(a, m+1, n)  ==>
        MultisetRetain{K,L}(a, n, v);
  }
*/

#endif /* MULTISETRETAIN_SPEC_INCLUDED */

