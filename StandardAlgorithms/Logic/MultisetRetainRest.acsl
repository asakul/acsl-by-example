
#ifndef MULTISETRETAINREST_ACSL_INCLUDED
#define MULTISETRETAINREST_ACSL_INCLUDED

#include "MultisetOperations.acsl"

/*@
  axiomatic MultisetRetainRest
  {
    predicate
    MultisetRetainRest{K,L}(value_type* a, integer n, value_type  v, value_type w) =
      \forall value_type x;
        x != v  ==>  x != w  ==>  MultisetRetain{K,L}(a, n, x);

    lemma Multiset_AddMinusRetain{K,L,M}:
      \forall value_type *a, u, integer n;
        MultisetAdd{K,L}(a, n, u)    ==>
        MultisetMinus{L,M}(a, n, u)  ==>
        MultisetRetain{K,M}(a, n, u);

    lemma Multiset_MinusAddRetain{K,L,M}:
      \forall value_type *a, u, integer n;
        MultisetMinus{K,L}(a, n, u) ==>
        MultisetAdd{L,M}(a, n, u)   ==>
        MultisetRetain{K,M}(a, n, u);

    lemma Multiset_AddMinusRetainReorder{K,L,M}:
      \forall value_type *a, u, v, integer n;
        u != v                               ==>
        MultisetAdd{K,L}(a, n, u)            ==>
        MultisetMinus{K,L}(a, n, v)          ==>
        MultisetRetainRest{K,L}(a, n, u, v)  ==>
        MultisetAdd{L,M}(a, n, v)            ==>
        MultisetMinus{L,M}(a, n, u)          ==>
        MultisetRetainRest{L,M}(a, n, v, u)  ==>
        MultisetReorder{K,M}(a, n);
  }
*/

#endif /* MULTISETRETAINREST_ACSL_INCLUDED */

