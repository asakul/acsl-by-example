
#ifndef MULTISETOPERATIONS_SPEC_INCLUDED
#define MULTISETOPERATIONS_SPEC_INCLUDED

#include "MultisetUnchanged.acsl"

/*@
  axiomatic MultisetOperations
  {
    predicate
    MultisetAdd{K,L}(value_type* a, integer n, value_type v) =
      Count{L}(a, 0, n, v) == Count{K}(a, 0, n, v) + 1;

    predicate
    MultisetMinus{K,L}(value_type* a, integer n, value_type v) =
      MultisetAdd{L,K}(a, n, v);

    lemma MultisetAdd_Distinct{K,L}:
      \forall value_type *a, v, integer i, n;
        0 <= i < n                         ==>
        \at(a[i],K) != v                   ==>
        \at(a[i],L) == v                   ==>
        MultisetUnchanged{K,L}(a, 0, i)    ==>
        MultisetUnchanged{K,L}(a, i+1, n)  ==>
        MultisetAdd{K,L}(a, n, v);

    lemma MultisetMinus_Distinct{K,L}:
      \forall value_type *a, v, integer i, n;
        0 <= i < n                         ==>
        \at(a[i],K) == v                   ==>
        \at(a[i],L) != v                   ==>
        MultisetUnchanged{K,L}(a, 0, i)    ==>
        MultisetUnchanged{K,L}(a, i+1, n)  ==>
        MultisetMinus{K,L}(a, n, v);
  }
*/

#endif /* MULTISETOPERATIONS_SPEC_INCLUDED */

